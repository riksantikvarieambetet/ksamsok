buildscript {
    repositories {
        maven { url 'https://artifactory.raa.se/artifactory/raa-gradle-prod' }
    }
}

plugins {
    id "war"

    // OWASP - security vulnerability checker. Administrativt beroende avseende SEC-CHECK.
	// Här kör vi latest eftersom vi bara använder den här för att få ut säkerhetsrapporter.
	// https://plugins.gradle.org/plugin/org.owasp.dependencycheck
	id 'org.owasp.dependencycheck' version 'latest.release'

	// Dependencyupdates - Administrativt beroende avseende versionshantering av beroenden.
	// Här kör vi latest eftersom vi bara använder den här som stöd vid uppgradering av beroenden till projektet.
	// https://plugins.gradle.org/plugin/com.github.ben-manes.versions
	id 'com.github.ben-manes.versions' version 'latest.release'
}

// Set up environment start
loadConfiguration()
def loadConfiguration() {
    def environment = project.hasProperty('env') ? project.env : 'default'
    project.ext.set 'environment', environment
}
apply from: 'environment.gradle'
// Set up environment end


apply plugin: 'idea'
apply plugin: 'eclipse'
//and standard one
apply plugin: 'java'
apply plugin: 'org.owasp.dependencycheck'

java {
    toolchain {
            languageVersion.set(JavaLanguageVersion.of(11))
    }
}

repositories {
    maven { url 'https://artifactory.raa.se/artifactory/raa-gradle-prod' }
}

// För att få ut owasp-rapporterna i HTML och CSV
dependencyCheck {
    formats=['HTML', 'CSV']
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.19.0'
    implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.3.2'
    implementation (group: 'org.apache.solr', name: 'solr-solrj', version: '9.1.0')
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.19.0'

    implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.24'
    implementation group: 'org.springframework', name: 'spring-context', version: '5.3.24'
    implementation group: 'org.springframework', name: 'spring-web', version: '5.3.24'
    implementation group: 'org.apache.jena', name: 'jena-core', version: '4.6.1'
    implementation group: 'org.apache.jena', name: 'jena-arq', version: '4.6.1'
    implementation group: 'com.rometools', name: 'rome', version: '1.18.0'
    implementation group: 'com.rometools', name: 'rome-modules', version: '1.18.0'
    implementation group: 'org.z3950.zing', name: 'cql-java', version: '1.10'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    
    implementation group: 'org.geotools.xsd', name: 'gt-xsd-gml2', version: '28.0'
    implementation group: 'org.geotools', name: 'gt-epsg-wkt', version: '28.0'
    // geotools needs this to build
    implementation group: 'javax.vecmath', name: 'vecmath', version: '1.5.2'

    implementation group: 'joda-time', name: 'joda-time', version: '2.12.2'
    implementation group: 'org.json', name: 'json', version: '20220924'
    implementation (group: 'xalan', name: 'xalan', version: '2.7.2') {
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation group: 'org.dspace', name: 'oclc-harvester2', version: '1.0.0'
    runtimeOnly group: 'co.elastic.logging', name: 'log4j2-ecs-layout', version: '1.5.0'
    runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.5.1'
    runtimeOnly group: 'javax.media.jai', name: 'com.springsource.javax.media.jai.core', version: '1.1.3'
    runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.5'
    // när du vill uppgradera den här jstl-grejen så kolla att sidan /ksamsok/admin/orgAdmin fungerar - hagla lyckas inte idag
    runtimeOnly group: 'org.eclipse.jetty', name: 'apache-jstl', version: '10.0.12'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.3.24'
    testImplementation group: 'org.springframework', name: 'spring-jdbc', version: '5.3.24'
    testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.0.12'
}


// Bygg med -Penv=buildserver för att bygga en warfil från en byggserver, 
// -Penv=local för att bygga en warfil med lokala inställningar
// Utan -Penv byggs en warfil med inställningar från src/main/environment/default
war {
    dependsOn setUpEnvironmentConfiguration
    archiveFileName = 'ksamsok.war'
    metaInf {
        from "src/main/environment/common/META-INF"
        def myProps = new Properties()
        file("src/main/environment/${environment}/environment.properties").withInputStream {
            myProps.load(it)
        }
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: myProps)
    }
    webInf {
        from "src/main/environment/common/WEB-INF"
        def myProps = new Properties()
        file("src/main/environment/${environment}/environment.properties").withInputStream {
            myProps.load(it)
        }
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: myProps)
    }
    manifest.attributes('Multi-Release': 'true')
}
